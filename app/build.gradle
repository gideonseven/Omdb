plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'com.google.dagger.hilt.android'
}

android {
    namespace "com.don.omdb"
    compileSdkVersion 34

    defaultConfig {
        applicationId "com.don.omdb"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
    kotlinOptions { jvmTarget = "21" }
    buildFeatures { viewBinding true }

    packaging {
        resources {
            // Choose the first copy when duplicates occur
            pickFirsts += ["META-INF/versions/9/OSGI-INF/MANIFEST.MF"]
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
}

dependencies {

    // Required for `by viewModels()` and `by activityViewModels()`
    implementation "androidx.activity:activity-ktx:1.9.3"

    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"

    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // Hilt (match plugin version)
    implementation "com.google.dagger:hilt-android:$hilt_version"   // 2.52
    kapt "com.google.dagger:hilt-compiler:$hilt_version"            // 2.52

    // Glide
    implementation("com.github.bumptech.glide:glide:$glide_version") { exclude group: "androidx" }
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    implementation "com.google.android.material:material:$material_version"
    debugImplementation "com.chimerapps.niddler:niddler:$niddler_version"

    // Retrofit/OkHttp
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_logging_version"

    implementation "com.jakewharton.timber:timber:$timber_version"
}

kapt {
    correctErrorTypes = true
}
